buildscript {
    ext.kotlin_version = '0.11.91.4'
    ext.android_support = '22.1.1'
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.15.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    lintOptions {
        disable 'MissingTranslation'
    }
    defaultConfig {
        applicationId "fr.geobert.radis"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 58
        versionName "1.1.8"
        testApplicationId "fr.geobert.radis.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'LICENSE.txt'
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:21.0.3'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$android_support"
    compile "com.android.support:support-v4:$android_support"
    compile "com.android.support:recyclerview-v7:$android_support"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.darwinsys:hirondelle-date4j:1.5.1"

    androidTestCompile 'com.android.support.test:runner:0.2'
    androidTestCompile('com.android.support.test:rules:0.2') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.1') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.1') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

//    androidTestCompile('com.android.support.test:testing-support-lib:0.1') {
//        exclude group: 'com.android.support', module: 'support-v4'
//        //    exclude group: 'org.hamcrest'
//    }

    compile('com.crashlytics.sdk.android:crashlytics:2.2.4@aar') {
        transitive = true;
    }
}
